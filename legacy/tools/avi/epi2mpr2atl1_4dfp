#!/bin/csh -f
#$Header: /data/petsun4/data1/solaris/csh_scripts/RCS/epi2mpr2atl1_4dfp,v 1.1 2004/06/29 02:43:47 avi Exp avi $
#$Log: epi2mpr2atl1_4dfp,v $
# Revision 1.1  2004/06/29  02:43:47  avi
# Initial revision
#
set idstr = '$Id: epi2mpr2atl1_4dfp,v 1.1 2004/06/29 02:43:47 avi Exp avi $'

echo $idstr
set program = $0; set program = $program:t
if (${#argv} < 2) then
	echo "Usage:	"$program" <epi_anat> <mpr_anat> [useold] [711-2<B|C|O|Y|K|L>]"
	echo "e.g.:	"$program" 011220_vc8221_anat_ave 011220_vc8221_3739-2"
	echo "N.B.:	named image files may include a path"
	echo "N.B.:	named images must be in either ANALYZE 7.5 or 4dfp format; ANALYZE will be converted to 4dfp"
	echo "N.B.:	the default target is 711-2B"
	exit 1
endif

set date0	= `date`
set usr		= `whoami`

@ useold	= 0
@ debug		= 0

set wrkdir	= $cwd
if (! $?RELEASE) then
	set bindir = /data/petsun4/data1/solaris
else
	set bindir = $RELEASE
endif
if (! $?REFDIR)	then
	set refdir = /data/petsun9/data2/petmri
else
	set refdir = $REFDIR
endif
set oristr	= (T C S)
set modes	= (4099 4099 3075 2059 2059)

set xxx = ($1 $2)
set xxx = ($xxx:gr)
set xxx = ($xxx:gr)
set tmp = ($xxx:gh)
set epi = $xxx[1]:t
if (-d $tmp[1]:h) then
	set epidir = `pushd $tmp[1]:h | awk '{print $1}'`
else
	set epidir = $cwd
endif
set mpr = $xxx[2]:t
if (-d $tmp[2]:h) then
	set mprdir = `pushd $tmp[2]:h | awk '{print $1}'`
else
	set mprdir = $cwd
endif

@ k = 1
while ($k <= 2)
	     if (-e $xxx[$k]:t.4dfp.img && -e $xxx[$k]:t.4dfp.ifh) then
	else if (-e $xxx[$k]:t.img && -e $xxx[$k]:t.hdr) then
		analyzeto4dfp $xxx[$k]:t
	else if (-e $xxx[$k]:t.4dint.img && -e $xxx[$k]:t.4dint.hdr) then
		analyzeto4dfp $xxx[$k]:t.4dint
	else if (-e $xxx[$k].4dfp.img && -e $xxx[$k].4dfp.ifh) then
		foreach e (img img.rec ifh)
			ln -s $xxx[$k].4dfp.$e .
		end
	else if (-e $xxx[$k].img && -e $xxx[$k].hdr) then
		/bin/cp $xxx[$k].{img,img.rec,hdr} .
		analyzeto4dfp $xxx[$k]:t
		/bin/rm $xxx[$k]:t.{img,img.rec,hdr}
	else if (-e $xxx[$k].4dint.img && -e $xxx[$k].4dint.hdr) then
		/bin/cp $xxx[$k].4dint.{img,img.rec,hdr} .
		analyzeto4dfp $xxx[$k]:t.4dint
		/bin/rm $xxx[$k]:t.4dint.{img,img.rec,hdr}
	else	echo $xxx[$k] not found
		exit -1
	else
	endif
	@ k++
end

set target = "711-2B"
foreach x (${argv[2-]})
	if ($x == "debug")	@ debug++
	if ($x == "useold")	@ useold++		# use existing t4files if present
	if ($x == "711-2C")	set target = $x		# np292 combined old+young
	if ($x == "711-2O")	set target = $x		# np292 old
	if ($x == "711-2Y")	set target = $x		# np292 young
	if ($x == "711-2K")	set target = $x		# 12 selected 789yo children
	if ($x == "711-2L")	set target = $x		# 12 selected 789yo children + 12 selected np364 young adults
end

set log = $mpr"_"$program.log
touch				$log
date >>				$log
echo $0 $argv[1-] >>		$log
echo $idstr >>			$log
echo epi = $epidir/$epi >>	$log
echo mpr = $mprdir/$mpr >>	$log
tail -50 $log

if ($debug) set echo
set t4file = $mpr"_to_"$target"_t4"
if ($useold && ! -e $t4file && -e $mprdir/$t4file) cp $mprdir/$t4file .
if (! $useold || ! -e $t4file) mpr2atl_4dfp $mpr $target

set t4file = $epi"_to_"$mpr"_t4"
if ($useold && -e $epidir/$t4file) /bin/cp $epidir/$t4file .
if (! $useold || ! -e $t4file) then
	msktgen_4dfp $mpr
	if ($status < 0) exit $status

	@ ori = `awk '/orientation/{print $NF - 1}' $mpr.4dfp.ifh`
	t4_inv $bindir/$oristr[$ori]_t4 temp$$_t4
	@ ori = `awk '/orientation/{print $NF - 1}' $epi.4dfp.ifh`
	t4_mul $bindir/$oristr[$ori]_t4 temp$$_t4 $t4file

	@ k = 1
	while ($k <= ${#modes})
	echo	imgreg_4dfp $mpr $mpr"_mskt" $epi none $t4file $modes[$k] >> $log
		imgreg_4dfp $mpr $mpr"_mskt" $epi none $t4file $modes[$k] >> $log
		if ($status < 0) exit $status
		@ k++
	end
	if (! $debug) /bin/rm $mpr"_msk"* temp$$_t4
endif

t4_mul $epi"_to_"$mpr"_t4" $mpr"_to_"$target"_t4"	$epi"_to_"$target"_t4"

echo "start time "$date0
echo "end   time "`date`
exit
